buildscript {
	repositories {
		mavenCentral()
		jcenter()
	}

	dependencies { classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2' }
}

apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

dependencies {
	compile gradleApi()
	compile localGroovy()
	compile 'org.eclipse.persistence:eclipselink:2.7.3'
	compile 'com.sun.xml.bind:jaxb-core:2.2.11'
	compile 'com.sun.xml.bind:jaxb-xjc:2.2.11'
	compile 'com.sun.xml.bind:jaxb-impl:2.2.11'
	compile 'javax.xml.bind:jaxb-api:2.2.12-b141001.1542'
	compile 'gradle-templates:gradle-templates:1.5'
	compile 'junit:junit:4.4'

	runtime 'org.eclipse.persistence:org.eclipse.persistence.moxy:2.7.3'
	runtime 'gradle-templates:gradle-templates:1.5'
	runtime 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.4'
	runtime 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
}

repositories {
	mavenCentral()
	jcenter()
	maven { url 'http://dl.bintray.com/ecpluginsdev/maven' }
}

ext {
	// Use empty bintray key in case it's not defined in gradle.properties
	bintrayKey = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : ''
}

// Set generated plugin class files to be compatible with JDK6
tasks.withType(GroovyCompile) {
	sourceCompatibility = 1.6
	targetCompatibility = 1.6
}

group='com.electriccloud.plugins'
version='2.2'

task wrapper(type: Wrapper) { gradleVersion = '2.13' }

publishing {
	repositories { maven { url "$buildDir/repo" } }

	publications {
		maven(MavenPublication) { from components.java }
	}
}

bintray {
	user = 'ecpluginsdev'
	key = bintrayKey
	publish = true
	dryRun = false
	publications = ['maven']

	pkg {
		repo = 'maven'
		name = project.name
		desc = 'A gradle plugin that houses the logic to build ElectricFlow plugins'
		websiteUrl = 'http://electric-cloud.com'
		licenses = ['mit']
		publicDownloadNumbers = true
		vcsUrl = 'https://github.com/electriccommunity/flow-gradle-plugin.git'
	}
}
